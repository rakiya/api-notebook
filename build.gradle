import nu.studer.gradle.jooq.JooqEdition

buildscript {
    ext {
        joobyVersion = "2.9.2"
        koinVersion = "2.1.6"
        jooqVersion = "3.13.4"
    }

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.10"
        classpath "io.jooby:jooby-gradle-plugin:$joobyVersion"
        classpath "org.koin:koin-gradle-plugin:$koinVersion"
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.4.0"
    id "nu.studer.jooq" version '5.0.2'
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id "application"
    id "io.jooby.run" version "${joobyVersion}"
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

apply plugin: "org.jetbrains.kotlin.kapt"
apply plugin: 'koin'

group "habanero"
version "0.0.1"

mainClassName = "habanero.MainKt"

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/konform-kt/konform" }
}

dependencies {
    // Kotlin (Language)
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.9'

    // Jooby (Web framework)
    implementation "io.jooby:jooby-netty:$joobyVersion"
    implementation "io.jooby:jooby-pac4j:$joobyVersion"
    implementation "io.jooby:jooby-gson:$joobyVersion"
    kapt "io.jooby:jooby-apt:$joobyVersion"

    // Pac4J (Security)
    implementation "org.pac4j:pac4j-http:4.0.3"

    // Koin (Dependency Injection)
    implementation "org.koin:koin-core:$koinVersion"

    // JOOQ (OR mapper)
    implementation "org.jooq:jooq:$jooqVersion"
    implementation "org.jooq:jooq-parent:$jooqVersion"
    implementation "org.jooq:jooq-meta"
    implementation "org.jooq:jooq-meta-extensions:$jooqVersion"
    implementation "org.jooq:jooq-codegen:$jooqVersion"
    implementation "org.jooq:jooq-codegen-maven:$jooqVersion"
    jooqGenerator "mysql:mysql-connector-java:8.0.21"

    // Mysql Connection
    implementation "mysql:mysql-connector-java:8.0.21"

    // JWT
    implementation "com.auth0:java-jwt:3.10.3"

    // Konform (Validation)
    implementation 'io.konform:konform:0.1.0'

    // Hibernate Validator (Validation)
    implementation 'org.hibernate.validator:hibernate-validator:6.1.5.Final'
    implementation 'javax.el:javax.el-api:3.0.0'
    implementation 'org.glassfish.web:javax.el:2.2.6'


    // HTTP Client
    implementation 'com.github.kittinunf.fuel:fuel:2.2.3'

    // Logback (Logging)
    implementation 'ch.qos.logback:logback-classic:1.2.3'

    // Test
    testImplementation "io.jooby:jooby-test:$joobyVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "com.nhaarman:mockito-kotlin:1.5.0"
    testImplementation "com.github.tomakehurst:wiremock:2.27.1"
    testImplementation "org.koin:koin-test:$koinVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

shadowJar {
    archiveFileName="api-notebook.${extension}"
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

test {
    useJUnitPlatform()
}

jooq {
    //jooqのバージョンを指定
    version = "$jooqVersion"
    edition = JooqEdition.OSS
    configurations {
        main {
            generationTool {
                logging = org.jooq.meta.jaxb.Logging.WARN
                jdbc {
                    driver = 'com.mysql.cj.jdbc.Driver'
                    url = 'jdbc:mysql://0.0.0.0:3306/api-notebook?serverTimezone=JST'
                    user = 'api'
                    password = 'abc'
                }
                generator {
                    name = 'org.jooq.codegen.DefaultGenerator'
                    database {
                        name = 'org.jooq.meta.mysql.MySQLDatabase'
                        inputSchema = 'api-notebook'
                    }
                    generate {
                        deprecated = false
                        records = true
                        immutablePojos = false
                        fluentSetters = true
                    }
                    target {
                        packageName = 'habanero.extensions.database'
                        directory = 'src/main/java'
                    }
                    strategy.name = 'org.jooq.codegen.DefaultGeneratorStrategy'
                }
            }
        }
    }
}
